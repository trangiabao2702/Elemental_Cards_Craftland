import "Hud.fcc" as Hud
import "Workflow.fcc" as Workflow
import "StdLibrary.fcc" as StdLib
import "EditorGenLib.fcc" as EditorLib

import "./Player/PlayerEntity.fcg" as PlayerEntity
import "./Card.fcg" as Card

graph GameManager {
    event OnPhaseStart(phase entity<Phase>) {
        var phaseIndex int = phase<Phase>.Index
        if phaseIndex == EPhase.Phase1Start || phaseIndex == EPhase.Phase2Start {
            LogWarning("PHASE START")
        } else if phaseIndex == EPhase.Phase1SelectCard || phaseIndex == EPhase.Phase2SelectCard {
            LogWarning("PHASE SELECT CARD")
        } else if phaseIndex == EPhase.Phase1Duel || phaseIndex == EPhase.Phase2Duel {
            LogWarning("PHASE DUEL")
        } else if phaseIndex == EPhase.Phase1End || phaseIndex == EPhase.Phase2End {
            LogWarning("PHASE END")
        } else {}
    }

    event OnDuelCardSet(player entity<Player>, opponent entity<Player>) {
        LogInfo("[GameManager] Receive event OnDuelCardSet: " + player<Player>.NickName + " " + opponent<Player>.NickName)
        var playerDuelCard entity<UIWidget> = player<PlayerEntity>.GetDuelCard()
        var opponentDuelCard entity<UIWidget> = opponent<PlayerEntity>.GetDuelCard()

        var result CompareEnum = CompareCards(playerDuelCard, opponentDuelCard)
        if result == CompareEnum.Win {
            LogInfo("[GameManager][OnDuelCardSet] Player Win: " + player<Player>.NickName)
            DispatchEventWithPlatform(OnPlayerClaimedSignal, player<PlayerEntity>.GetDuelHUD(), PlatformType.Local, List<object>{playerDuelCard, true})
        } else if result == CompareEnum.Lose {
            LogInfo("[GameManager][OnDuelCardSet] Player Lose: " + player<Player>.NickName)
            DispatchEventWithPlatform(OnPlayerClaimedSignal, player<PlayerEntity>.GetDuelHUD(), PlatformType.Local, List<object>{opponentDuelCard, false})
        } else {
            LogInfo("[GameManager][OnDuelCardSet] Draw")
        }
    }

    // Win = 1; Draw = 0; Lose = -1
    func CompareCards(cardA entity<UIWidget>, cardB entity<UIWidget>) CompareEnum {
        var resultCompareElements CompareEnum = CompareElements(cardA<Card>.GetElement(), cardB<Card>.GetElement())
        if resultCompareElements != CompareEnum.Draw {
            return resultCompareElements
        }

        var resultComparePowerPoints CompareEnum = ComparePowerPoints(cardA<Card>.GetPowerPoint(), cardB<Card>.GetPowerPoint())
        return resultComparePowerPoints
    }

    func CompareElements(elementA string, elementB string) CompareEnum {
        if elementA == elementB {
            return CompareEnum.Draw
        }

        if elementA == ElementEnum.Fire && elementB == ElementEnum.Water {
            return CompareEnum.Lose
        }
        
        if elementA == ElementEnum.Water && elementB == ElementEnum.Wood {
            return CompareEnum.Lose
        }
        
        if elementA == ElementEnum.Wood && elementB == ElementEnum.Fire {
            return CompareEnum.Lose
        }
        
        return CompareEnum.Win
    }

    func ComparePowerPoints(powerPointA int, powerPointB int) CompareEnum {
        if powerPointA > powerPointB {
            return CompareEnum.Win
        } else if powerPointA < powerPointB {
            return CompareEnum.Lose
        } else {
            return CompareEnum.Draw
        }
    }
}